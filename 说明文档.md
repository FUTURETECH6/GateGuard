

# 具备访客识别与用户认证功能的门禁系统





## 设计思路

* #### 理念

  * 以较廉价的工具实现较可靠的认证功能。
  
* #### 思路

  * 由于不同人在无意识情况下敲门会有一定的节奏，力度，敲门方式(敲或拍)，从而造成同个门上振动传感器接受的数据不同，进行量化处理后可以作为身份识别的特征；同时因为超声波传感器测距精度较高，可以以身高作为另一特征；如此进行双重认证效果较好，可以让管理员不开门就知道访客身份。
  * 此外，管理员本人经过认证后再输入密码也可作为不需钥匙开门的方式。

* #### 布局

  * 超声波传感器与红外热释传感器放置在门外侧上方；
  * 键盘矩阵固定在门外侧；
  * 面包板、Arduino UNO、蓝牙模块、74HC595N布置在门内侧任意位置，布线方便即可；
  * LCD显示器放在需要的位置；
  * 伺服电机布置在与锁水平处；
  * 数字振动传感器布置在门中心处，可以提高灵敏度；
  * 若使用声音传感器，则尽量放在噪声干扰较小而仍可探测到敲门声的地方。



## 器材及使用事项

1. #### Arduino UNO

2. #### 数字震动传感器(二选一)

   * 使用1个I/O引脚
   * 无振动保持高电平输出，震动时输出低电平
   * 注意事项：需使用中断触发机制
   * 链接：[购买链接](http://www.dfrobot.com.cn/goods-76.html)

3. #### 声音传感器(二选一)

   * 使用1个I/O引脚
   * 环境声音低于阈值时保持低电平输出，超过阈值时输出高电平
   * 阈值可通过传感器模块上的电位器调节

4. #### 超声波传感器

   * 使用2个I/O引脚

5. #### 数字人体红外热释电运动传感器

   * 使用1个I/O引脚

   * 工作原理：
     
     人体都有恒定的体温，一般在37度，所以会发出特定波长10UM左右的红外线，被动式红外探头就是靠探测人体发射的10UM左右的红外线而进行工作的。人体发射的10UM左右的红外线通过菲泥尔滤光片增强后聚集到红外感应源上。红外感应源通常采用热释电元件，这种元件在接收到人体红外辐射温度发生变化时就会失去电荷平衡，向外释放电荷，后续电路经检测处理后就能产生报警信号。
     
     * 热释电效应：
       
       当一些晶体受热时，在晶体两端将会产生数量相等而符号相反的电荷。这种由于热变化而产生的电极化现象称为热释电效应。
     
     * 菲涅耳透镜：
     
       根据菲涅耳原理制成，菲涅耳透镜分为折射式和反射式两种形式，其作用一是聚焦作用，将热释的红外信号折射（反射）在PIR上；二是将检测区内分为若干个明区和暗区，使进入检测区的移动物体能以温度变化的形式在PIR上产生变化热释红外信号，这样PIR就能产生变化电信号。使热释电人体红外传感器(PIR)灵敏度大大增加。
     
   * 触发方式：
     
     L不可重复，H可重复。可改变跳线帽位置选择。
     
     * 不可重复触发方式：即感应输出高电平后，延时时间一结束，输出将自动从高电平变为低电平。
     * 可重复触发方式： 即感应输出高电平后，在延时时间段内，如果有人体在其感应范围内活动，其输出将一直保持高电平，直到人离开后才延时将高电平变为低电平(感应模块检测到人体的每一次活动后会自动顺延一个延时时间段，并且以最后一次活动的时间为延时时间的起始点)。
     
     本实验采用可重复触发方式。
     
   * 工作参数： 
     
     * 工作电压：3～5V
     * 静态电流：50μA

6. #### 74HC595N

   * 作为LCD与Arduino的中继，减少引脚用量
   * 使用3个I/O引脚

7. #### LCD显示器

   * 采用四线接法，接到74HC595N上
   * 将背光源正极接至Arduino AVR的I/O引脚，以便于闲时可以关闭LCD的背光

8. #### 4x4键盘矩阵

   * 使用8个I/O引脚

9. #### 伺服电机

   * 使用1个I/O引脚

10. #### 扬声器

   * 使用1个I/O引脚

11. #### 插销门锁

    * 通过细线与伺服电机相连，解锁时可被伺服电机拉开，但上锁需手动进行

12. #### DFRduino UNO R3 拓展板

    * 有现成的Vcc与Gnd引脚，可以简化电路
    * [购买链接](https://item.taobao.com/item.htm?spm=a1z09.2.0.0.50cc2e8dZKLnLN&id=558805665857&_u=j25ghquqb339)

13. #### 其他

    1. 面包板
    2. 面包线(长线)
    3. 电源适配器及供电线(测试时由于长度不够未采用，但实际应用中若要脱离电脑单独运行则需要)



## 使用说明

* 利用红外热释传感器的可重复触发模式判断是否有人进入待测区域；

* 上方的超声波传感器可以测量人身高数据；

* 震动传感器/声音传感器可以测出敲门的节奏，强度等数据，并在<u>一定容错率</u>条件下与数据库进行匹配；

  * 宏定义中的 useVib，1表示使用震动传感器，0表示使用声音传感器
  * 宏定义中的 ignoreVtime，1表示忽略震动时间，即不考虑震动强度，0相反

* 节奏转化为数字格式可以通过记录有震动与没震动的时间段占比实现；

* 若是人在敲门，则会被测量身高信息与敲门参数，并与数据库对比。若有信息，则可以直接在LCD屏幕上显示访客信息，并通过蓝牙发送至电脑显示(若电脑接收端开启)；若无信息，则可记录下访客记录，后续可以在电脑上进行登记；

  * 身高可能会受鞋高、有无戴帽子、头发长度等的影响
    * 允许最大身高误差通过宏定义中 MAX_VALID_DIFF_HEIGHT 修改，单位cm
    * 屋顶高度不一定要为真实值，只需始终保持不变即可，通过宏定义中 ROOF_HEIGHT 修改，单位为cm
  * 同一个人每次敲门节奏不会完全相同
    * 两次敲门间时间的最大允许误差通过宏定义中 MAX_VALID_DIFF_NonVibration_Duration 修改，单位为ms
    * 敲门震动时间的最大允许误差通过宏定义中的 MAX_VALID_DIFF_Vibration_Duration，单位为ms

* 键盘矩阵上的按键可用作门铃或密码输入器；

  * 对于访客，除了#以外的按键均可作为门铃，按下后会激活门铃音乐
  * 对于管理员，按下#开始输入密码，再按下一次#结束输入

* 若要开锁，则需满足身高与数据库中匹配，能在门上敲出正确节奏，且输入正确密码这三个条件；

  * 建议以上三步骤按先后顺序完成，因为若用震动传感器且只记录一次敲门，则输入密码时的震动会触发记录

* 若有人长时间停留且未敲门，扬声器会报警；

  * 时限通过宏定义中的 WORKTIME 调节，单位ms
  * 警报时间通过宏定义中的 RedAlert_Time 调节，单位s

* 数据存储在Arduino UNO的EEPROM上；

* 编译上传Arduino_FINAL_InfoRecord.ino后，可以在串口监视器中对数据进行操作；

  具体如下(运行时会有提示)：

  * 发送1：阅览全部数据
  * 发送0：修改数据
    * 发送1：改名
      * 输入编号(地址编号)与要改的名字
    * 发送0：删除
      * 输入编号(地址编号)
  * 完成一次操作后
    * 发送1：继续进行操作
    * 发送0：退出
  * 由于Arduino的EEPROM可擦写次数有限，不能将擦写操作放在loop()中，因此若要再进行修改需重新上传程序



## 代码与说明

#### 源码

###### 头文件 Parameter.h，存储所需参数

```c++
#include <LiquidCrystal_74HC595.h>
#include <EEPROMex.h>
#include <Keypad.h>
#include <LiquidCrystal.h>
#include <Servo.h>

#define pinTrig 4
#define pinEcho 5

#define pinInfrared 3

#define useVib 0
#define ignoreVtime 1
#define pinVibration 2
#define pinSound 2  //must be 2 or 3

#define pinSpeaker 6

#define pinServo 7

#define pinLatch 12
#define pinClock 13
#define pinData 11

#define pinLCD_A 10

//const parameter
#define EEPORM_SIZE 1024
#define ROOF_HEIGHT 191  //cm
#define Name_Display_Time 100000

//parater concerning verifying
#define MAX_PASSWORD_LENGTH 20
#define ValidDistance 200  //A Parameter Used in getHeight()
#define MAX_KNOCKS_TIMES 10
#define KnockStopsTime 2000
#define RedAlert_Time 10  //s
#define WORKTIME 30000  //Working Time after Someone coming
#define REFRESHING_PERIOD 2000  //Refreshing Perion of the Infrared Motion Sensor
#define UNLOCK_TIME 5000  //The time during which the door is unlocked, but you need to put the latch back mannually
#define MAX_VALID_DIFF_HEIGHT 3  //cm, the max difference between the recorded height and the measured height
#define MAX_VALID_DIFF_Vibration_Duration 5
#define MAX_VALID_DIFF_NonVibration_Duration 50
#define MIN_KnockSound 300  //of 1024
#define MAX_NAME_SIZE 10

const int c = 261;  //used in doorbell music
const int d = 294;
const int e = 329;
const int f = 349;
const int g = 391;
const int gS = 415;
const int a = 440;
const int aS = 455;
const int b = 466;
const int cH = 523;
const int cSH = 554;
const int dH = 587;
const int dSH = 622;
const int eH = 659;
const int fH = 698;
const int fSH = 740;
const int gH = 784;
const int gSH = 830;
const int aH = 880;

typedef struct
{
    _Bool isInUse;  //So as you can delete a info that you dont want and cover it with new info
    char name[MAX_NAME_SIZE + 1];
    byte height;
#if useVib
    int knock[2 * MAX_KNOCKS_TIMES - 1]; //knock[0] stands for the first duration of vibration
#else
    int knock[MAX_KNOCKS_TIMES - 1];
#endif
} info;
```



###### 控制系统运行的源码 Arduino_FINAL_CONTROL.ino

```c++
#include "Parameter.h"

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
/*
   Function
*/

//Basic Indentification
void startKnock(void);
void getVibration(void);
void getSound(void);
byte getHeight(void);
//Doorbell
void firstSection(void);
void beep(int note, int duration);
void ringDoorbell(void);
void stopDoorbell(void);
void keypadEvent(KeypadEvent key);
//Administrator Identification
char* getPasswordInput(void);  //return a string<u> starts and<\u> ends with '#'
_Bool isRightPassword(char* input);
void wrongPasswordAlert(void);
//Reaction
byte compareVibrationAndHeight(void);  //If found, return the address of the info in EEPROM starting from 0. Otherwise, return -1.
void displayOrStoreInfo(void);
char* findName(byte address_EEPROM);
void projectToLCD(char *pName);
void unlock(void);
void redAlert(void);

/*
   Global Variables
*/
char password[] = "123ABCD#";
byte validNumber_EEPROM;
char keypadInput[MAX_PASSWORD_LENGTH + 1] = {0};
int counter;  //music
info thisInfo;  //store info. of current vistor
_Bool noKnock = 1;
_Bool isKnocking = 0;  //Used in Interrupt
_Bool passDoubleIdentification = 0;
_Bool isHoldKey = 0;
byte address_First_Unused_Unit;  //***likes 0 1 2 3 4 5 6
unsigned int tempT;


/*
   Sensor and Worker Settings
*/
Servo myServo;
LiquidCrystal_74HC595 myLcd(11, 13, 12, 1, 3, 4, 5, 6, 7);  //rs, e, d4-d7
char keys[4][4] = {{'1', '2', '3', 'A'}, {'4', '5', '6', 'B'}, {'7', '8', '9', 'C'}, {'*', '0', '#', 'D'}};
byte rowPins[4] = {8, 9, A5, A4};
byte colPins[4] = {A3, A2, A1, A0};
Keypad myKeypad = Keypad(makeKeymap(keys), rowPins, colPins, 4, 4);


//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
/*
   Functional Function
*/
///////////////////////////////////////////////////////////////////////////////////////
void startKnock(void) {
  detachInterrupt(digitalPinToInterrupt(pinSound));  //Prevent the recording processing for being interrupted. Also detach after first knock
  Serial.println("Sound dectected");
  isKnocking = 1;  //make the loop() reaches the process of recording height as quick as possible(jump getHeight())
  tempT = millis();  //keep the time before being interrupted
}
void getVibration() {
  if (!noKnock) return;  //Prevent from overwriting data, or recording the vibration of opening the door
  noKnock = 0;
  int tempInitTime = millis() % 65536, tempLastTime = tempInitTime;
  byte p;
  _Bool Vstatue = 1; //1 for V, 0 for nonV
  int knockInfo[2 * MAX_KNOCKS_TIMES - 1] = {0};

  int tempT = millis() % 65536;
  while ( (tempT > tempLastTime ? tempT - tempLastTime : 65536 - tempLastTime + tempT) > KnockStopsTime && p < MAX_KNOCKS_TIMES) {
    if (Vstatue == 0 && digitalRead(pinVibration) == 1) {  //Starts vibration
      knockInfo[p++] = tempT > tempLastTime ? tempT - tempLastTime : 65536 - tempLastTime + tempT;
      tempLastTime = tempT;
      Vstatue = 1;
    }
    else if (Vstatue == 1 && digitalRead(pinVibration) == 0) {  //Stops vibration
      knockInfo[p++] = tempT > tempLastTime ? tempT - tempLastTime : 65536 - tempLastTime + tempT;
      tempLastTime = tempT;
      Vstatue = 0;
    }
    tempT = millis() % 65536;
  }
}

void getSound(void) {
  if (!noKnock) return;  //Prevent from overwriting data, or recording the vibration of opening the door
  noKnock = 0;
  unsigned int tempLastTime = tempT, dT = 0;
  byte p = 0;
  byte count = 0;
  _Bool Vstatue = 0; //1 for V, 0 for nonV
  int knockInfo[MAX_KNOCKS_TIMES - 1] = {0};
#if ignoreVtime
  delay(100);  //prevent from recording the PWM
  while ( dT < KnockStopsTime && count < MAX_KNOCKS_TIMES - 1) {
    if (digitalRead(pinSound) == 0 && dT > 100) { //Starts vibration
      knockInfo[count++] = dT;
      tempLastTime = tempT;
    }
    tempT = millis() % 65536;
    dT = tempT > tempLastTime ? tempT - tempLastTime : 65536 - tempLastTime + tempT;
  }
#else
  while ( dT < KnockStopsTime && count < MAX_KNOCKS_TIMES) {
    if (Vstatue == 0 && digitalRead(pinSound) == 1) {  //Starts vibration
      knockInfo[count++] = dT;
      Vstatue = 1;
      count++;
    }
    else if (Vstatue == 1 && digitalRead(pinSound) == 0) {  //Stops vibration
      tempLastTime = tempT;
      Vstatue = 0;
    }
    tempT = millis() % 65536;
    dT = tempT > tempLastTime ? tempT - tempLastTime : 65536 - tempLastTime + tempT;
  }
#endif

  for (int i = 0; i < MAX_KNOCKS_TIMES - 1/* && knockInfo[i]*/; i++) thisInfo.knock[i] = knockInfo[i];  //overwrite all unit in the array
  for (int i = 0; i < MAX_KNOCKS_TIMES - 1 && knockInfo[i]; i++) Serial.println(knockInfo[i]);
  isKnocking = 0;
  // attachInterrupt(digitalPinToInterrupt(pinSound), getSound, RISING);  //If allowed multiple times of knocking

  if (thisInfo.knock[0] == 0) {  //Prevent the case that the recorder being activated by noise
    noKnock = 1;
    attachInterrupt(digitalPinToInterrupt(pinSound), startKnock, FALLING);
  }
}


byte getHeight(void) {
  //Activate Ultrasonic Sensor
  digitalWrite(pinTrig, 0);
  delayMicroseconds(10);
  digitalWrite(pinTrig, 1);
  delayMicroseconds(10);
  digitalWrite(pinTrig, 0);
  int duration = pulseIn(pinEcho, 1);  //Records Ultrasonic Wave's propagation in μs
  byte distance = (byte)(duration * 0.0174);  //Compute distance from roof in cm
  if (distance < ROOF_HEIGHT && distance > 0)
    return ROOF_HEIGHT - distance; //Return valid height
  else return 0;  //Return Error Mark
}
///////////////////////////////////////////////////////////////////////////////////////
void firstSection(void)
{
  beep(a, 500);
  beep(a, 500);
  beep(a, 500);
  beep(f, 350);
  beep(cH, 150);
  beep(a, 500);
  beep(f, 350);
  beep(cH, 150);
  beep(a, 650);

  delay(500);

  beep(eH, 500);
  beep(eH, 500);
  beep(eH, 500);
  beep(fH, 350);
  beep(cH, 150);
  beep(gS, 500);
  beep(f, 350);
  beep(cH, 150);
  beep(a, 650);

  delay(500);
}

void beep(int note, int duration)
{
  tone(pinSpeaker, note);  //Play tone on pinSpeaker
  delay(duration);
  noTone(pinSpeaker);  //Stop tone on pinSpeaker
  delay(50);
  counter++;
}

void ringDoorbell(void) {
  counter = 0;
  firstSection();
}

void stopDoorbell(void) {
  digitalWrite(pinSpeaker, 0);
}

void keypadEvent(KeypadEvent key) {
  switch (myKeypad.getState()) {
    case PRESSED:
      isHoldKey = 1;
      break;

    case RELEASED:
      isHoldKey = 0;
      break;

    case HOLD:
      break;
  }
}
///////////////////////////////////////////////////////////////////////////////////////
char* getPasswordInput(void) {
  for (int i = 0; i < MAX_PASSWORD_LENGTH + 1; i++) keypadInput[i] = 0;
  int i = 0;
  for (; i < MAX_PASSWORD_LENGTH + 1;) {
    char key;
    while (1) {
      key = myKeypad.getKey();
      if (key != 0) break;
    }
    Serial.println(key);
    keypadInput[i] = key;
    if (keypadInput[i] == '#') {
      return keypadInput;
    }
    else i++;
  }
  Serial.println(keypadInput[0]);
  return keypadInput;
}

_Bool isRightPassword(char* input) {
  _Bool b = 1;
  int i;
  for (i = 0; password[i] != '#'; i++) {
    if (password[i] != keypadInput[i]) {
      b = 0;
      break;
    }
  }
  if (password[i] == '#' && keypadInput[i] != '#') {  //判断
    b = 0;
  }
  for (int i = 0; i < MAX_PASSWORD_LENGTH + 1; i++) keypadInput[i] = 0;
  if (b)Serial.println("RIGHTPASSWORD");
  return b;
}

void wrongPasswordAlert(void) {
  for (int i = 1200; i > 500; i--) {
    tone(pinSpeaker, i);
    delay(2);
  }
  noTone(pinSpeaker);
}
////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
byte compareVibrationAndHeight(void) {  //If found, return the address of the info in EEPROM starting from 0. Otherwise, return -1.
  _Bool isFirstUnused = 1;
  byte tempAddress = 0;
  for (int i = 0; i < validNumber_EEPROM; tempAddress++) {  //i is a counter
    info loopInfo;
    EEPROM.readBlock(sizeof(info) * tempAddress + 1, loopInfo);
    if (!loopInfo.isInUse) {  //avoid invalid EEPROM unit
      if (isFirstUnused) {
        address_First_Unused_Unit = tempAddress;
        isFirstUnused = 0;
      }
      continue;
    }
    i++;
    _Bool b = 1;
    //compare height
    if (abs(loopInfo.height - thisInfo.height) > MAX_VALID_DIFF_HEIGHT) {
      b = 0;
      Serial.println(tempAddress);
      Serial.println("height not match");
    }

    //compare knock
    if (b) {
#if ignoreVtime
      for (int j = 0; j < MAX_KNOCKS_TIMES - 1 && loopInfo.knock[j]; j++) {
        if ( abs(thisInfo.knock[j] - loopInfo.knock[j]) > MAX_VALID_DIFF_NonVibration_Duration) {  //Non-Vibration
          b = 0;
          break;
        }
        Serial.println("knock match");
      }
#else
      for (int j = 0; j < 2 * MAX_KNOCKS_TIMES - 1; j++) {
        if ( j % 2 == 0 && abs(thisInfo.knock[j] - loopInfo.knock[j]) > MAX_VALID_DIFF_Vibration_Duration ) {  //Vibration
          b = 0;
          break;
        }
        if (j % 2 == 1 && abs(thisInfo.knock[j] - loopInfo.knock[j]) > MAX_VALID_DIFF_NonVibration_Duration) {  //Non-Vibration
          b = 0;
          break;
        }
      }
#endif
      if (b) {
        passDoubleIdentification = 1;
        return tempAddress;
      }
      Serial.println(tempAddress);
      Serial.println("Knocks not match");
    }
  }  //for loop ends here
  if (isFirstUnused) address_First_Unused_Unit = (byte) - 1;  //Unused info unit not found
  return (byte) - 1;
}

void displayOrStoreInfo(void) {
  validNumber_EEPROM = EEPROM.read(0);
  Serial.print("validNumber_EEPROM = ");
  Serial.println(validNumber_EEPROM);
  int tempAddress_targetInfo = compareVibrationAndHeight();  //1,2,3,4...

  if (tempAddress_targetInfo != (byte) - 1) {
    projectToLCD(findName(tempAddress_targetInfo));
  }

  if (tempAddress_targetInfo == (byte) - 1) {
    projectToLCD("Unknown Guest");
    thisInfo.name[0] = '\0';
    thisInfo.isInUse = 1;
    if (address_First_Unused_Unit != (byte) - 1)
      EEPROM.writeBlock( (address_First_Unused_Unit) * sizeof(info) + 1 , thisInfo);
    else {
      EEPROM.writeBlock( (validNumber_EEPROM++) * sizeof(info) + 1 , thisInfo);
      EEPROM.write(0, validNumber_EEPROM);
    }
  }

  Serial.print("tempAddress_targetInfo = ");
  Serial.println(tempAddress_targetInfo);

  Serial.print("address_First_Unused_Unit = ");
  Serial.println(address_First_Unused_Unit);

  Serial.print("validNumber_EEPROM = ");
  Serial.println(validNumber_EEPROM);
}

char* findName(byte address_EEPROM) {
  static info temp;  //let it be static so that its name can be read from outside
  EEPROM.readBlock(sizeof(info) * address_EEPROM + 1, temp);  // !!! NOT SURE IF THIS IS OK>>  Done.
  return temp.name;
}

void projectToLCD(char *pName) {
  myLcd.clear();
  digitalWrite(pinLCD_A, 1);
  if (pName[0]) myLcd.print(pName);
  else myLcd.print("Known Guest");  //case that the vistor has been record but has not been named
}


void unlock(void) {
  myServo.attach(pinServo);
  for (int i = 160; i >= 20; i--) {
    myServo.write(i);
    delay(5);
  }
  myServo.detach();
  delay(UNLOCK_TIME);
  myServo.attach(pinServo);
  for (int i = 20; i <= 160; i++) {
    myServo.write(i);
    delay(5);
  }
  myServo.detach();
}

void redAlert(void) {
  for (int i = 0; i < RedAlert_Time; i++) {
    tone(pinSpeaker, 500);
    delay(500);
    noTone(pinSpeaker);
    delay(500);
  }
}

//////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
/*
   Main Function
*/

void setup() {
  Serial.begin(9600);  //For testing, disable for official version
  myLcd.begin(16, 2);
  pinMode(pinInfrared, 0);
  pinMode(pinSound, 0);
  pinMode(pinLatch, 1);
  pinMode(pinData, 1);
  pinMode(pinClock, 1);
  pinMode(pinLCD_A, 1);
  pinMode(pinSpeaker, 1);
  pinMode(pinTrig, 1);
  pinMode(pinEcho, 0);
  pinMode(pinServo, 1);
  //  myKeypad.addEventListener(keypadEvent);
}

///////////////////////////////////////////////////////////////////////////////////////

void loop() {  //Initialize
  _Bool canOpen = 0;
  _Bool hasUnlocked = 0;
  _Bool hasANewPassWordInput = 0;
  char *keypadInputAddress;
  for (int i = 0; i < MAX_PASSWORD_LENGTH + 1; i++) keypadInput[i] = 0;
  _Bool firstInto_displayOrStoreInfo = 1;  //Only record the first knock and the corresponding height. Prevent from writing on the EEPROM too frequently and damage it
  noKnock = 1;
  isKnocking = 0;
  passDoubleIdentification = 0;
  address_First_Unused_Unit = (byte) - 1;
  for (int i = 0; i < MAX_NAME_SIZE + 1; i++) thisInfo.name[i] = 0;
  for (int i = 0; i < MAX_KNOCKS_TIMES - 1; i++) thisInfo.knock[i] = 0;
  thisInfo.height = 0;
  myServo.attach(pinServo);
  myServo.write(160);
  delay(500);
  myServo.detach();

  //Wait for one's coming
  while (1) {
    delay(REFRESHING_PERIOD);
    if (digitalRead(pinInfrared))break;
  }

  //Initialize'
  unsigned int countTime = 0;
  unsigned int initTime = millis() % 65536;


  Serial.println("Human motion dectected");
  //Use Interrupt to Detect Kncok
#if useVib
  attachInterrupt(digitalPinToInterrupt(pinVibration), startKnock, FALLING);
#else
  attachInterrupt(digitalPinToInterrupt(pinSound), startKnock, FALLING);
#endif

  //Main Loop
  while (countTime < 30000 /*&& !hasUnlocked*/) {  //hasUnlocked 没用上， 看看需不需要admin的LCD显示很久
    if (!isKnocking) { //after the knock sensor being activated, shorten the time it takes to enter the recording function
      //Height
      if (!isKnocking) {
        int tempH = getHeight();  //Mid time-consuming
        // Serial.print("tempH = ");
        // Serial.println(tempH);
        if (tempH > thisInfo.height && tempH != -1) thisInfo.height = tempH;
      }

      //Keypad
      if (!isKnocking) {
        char tempK = myKeypad.getKey();
        switch (tempK) {
          case 0:
            stopDoorbell();
            break;

          case '#':
            keypadInputAddress = getPasswordInput();  //High time-consuming
            hasANewPassWordInput = 1;
            break;

          default:
            detachInterrupt(digitalPinToInterrupt(pinSound));
            ringDoorbell();  //High time-consuming
            attachInterrupt(digitalPinToInterrupt(pinSound), startKnock, FALLING);
        }
      }

      //Verify password input
      if (!isKnocking && hasANewPassWordInput) {
        if (isRightPassword(keypadInputAddress)) {
          if (passDoubleIdentification)
            unlock();
          break;
        }
        else {
          detachInterrupt(digitalPinToInterrupt(pinSound));
          wrongPasswordAlert();  //High time-consuming
          attachInterrupt(digitalPinToInterrupt(pinSound), startKnock, FALLING);
          for (int i = 0; i < MAX_PASSWORD_LENGTH + 1; i++) keypadInput[i] = 0;
        }
        hasANewPassWordInput = 0;
      }

      //Modify data in EEPROM
      if (!isKnocking && !noKnock && firstInto_displayOrStoreInfo) {  // !!! Assume that when someone got inside the area and knock, his height has been correctly recorded. WILL DISCUSS ABOUT THIS LATER>>
        displayOrStoreInfo();
        firstInto_displayOrStoreInfo = 0;
      }
    }

    //record knocking info
    if (isKnocking) {
#if useVib
      getVibration();
#else
      getSound();
#endif
      isKnocking = 0;
    }

    //Modify countTime in the End of a while() loop
    unsigned int tempT = millis() % 65536;
    countTime = tempT > initTime ? tempT - initTime : tempT + 65536 - initTime;
  }
  //After 30 seconds with no knock
  if (noKnock && digitalRead(pinInfrared) == 1) {
    redAlert();
  }

  myLcd.clear();
  digitalWrite(pinLCD_A, 0);  //stop the power supply of LCD after display so as to save energy
}
```



###### 进行数据读取的源码 Arduino_FINAL_InfoRecord.ino

```c++
#include "Parameter.h"

void writeName(char *dest, char *source) {
  int i;
  for (i = 0; * (source + i); i++) {
    *(dest + i) = *(source + i);
  }
  *(dest + i) = *(source + i);
}

void setup() {
  _Bool workMode;
  _Bool modiMode;
  Serial.begin(9600);
start:

  int validInfoNum = EEPROM.read(0);
  Serial.println("Please select woring mode, 1 for get vistors data, 0 for modify guests data.");
  Serial.println();
  while (Serial.available() == 0);
  workMode = Serial.read() == '1' ? 1 : 0;
  delay(50);
  if (workMode == 1) {
    Serial.print("validInfoNum = ");
    Serial.println(validInfoNum);
    int ads = 0;
    for (int i = 0; i < validInfoNum; ads++) {  //i is the counter, ads is the actual address
      info loopInfo;
      EEPROM.readBlock(sizeof(info) * ads + 1, loopInfo);
      if (loopInfo.isInUse) {
        i++;
        Serial.println();
        Serial.print("Address = ");
        Serial.println(ads);

        Serial.print("Name = ");
        for (int j = 0; loopInfo.name[j]; j++) Serial.print(loopInfo.name[j]);
        Serial.println();

        Serial.print("Height = ");
        Serial.println(loopInfo.height);

        Serial.print("Knock: ");
        for (int j = 0; j < MAX_KNOCKS_TIMES - 1 && loopInfo.knock[j]; j++) {
          Serial.print(loopInfo.knock[j]);
          Serial.print(' ');
        }
        Serial.println();
        Serial.println();
      }
    }
    Serial.println();
  }
  if (workMode == 0) {
    //    char tempName[MAX_NAME_SIZE + 1]={0};

    Serial.println("Please select modification mode, 1 for change name, 0 for delete data.");
    Serial.println();
    while (Serial.available() == 0);
    modiMode = Serial.read() == '1' ? 1 : 0;

    if (modiMode == 1) {
      Serial.println("Please input the Address of the data you want to modify.");
      Serial.println();
      while (Serial.available() == 0);
      int no = 0;
      while (Serial.available() > 0)
      {
        no = no * 10 + Serial.read() - '0';
        delay(2);
      }
      Serial.println("Please input the name");
      Serial.println();
      while (Serial.available() == 0);
      char tempName[MAX_NAME_SIZE] = {0};
      int i = 0;
      while (Serial.available() > 0)
      {
        tempName[i++] = char(Serial.read());
        delay(2);
      }
      EEPROM.writeBlock(sizeof(info) * no + 2, tempName);
    }

    if (modiMode == 0) {
      Serial.println("Please input the Address of the data you want to delete.");
      Serial.println();
      while (Serial.available() == 0);
      int no = 0;
      while (Serial.available() > 0)
      {
        no = no * 10 + Serial.read() - '0';
        delay(2);
      }
      EEPROM.write(0, --validInfoNum);
      EEPROM.writeBlock(sizeof(info) * no + 1, (_Bool)0);

    }

  }
  Serial.println("Still need to read or write? 1 for yes, 0 for no");
  Serial.println();
  while (Serial.available() == 0);
  if (Serial.read() == '1') {
    delay(5);
    goto start;
  }

  delay(5);
}

void loop() {}
```



#### 部分参数与变量作用说明

* \#define useVib

  是否使用震动传感器，是为1，否(用麦克风)为0。

* \#define ignoreVtime

  是否忽略门持续震动的时间，是为1，否为0。

* byte validNumber_EEPROM

  存储于EEPROM的第一个字节中，记录EEPROM中有效info的个数。需注意的是由于删除功能的存在，个数与地址并不对应。

* byte address_First_Unused_Unit

  因为在数据操作时会删除数据，因此会造成存储单元之间的空隙。该变量记录EEPROM中第一个空闲的info单元的地址，便于之后新的数据的存储，可以压缩存储所需空间。当为255时表示有效数据之间不存在空隙。

* _Bool noKnock

  表示激活后是否未敲门。用于判断是否进入`displayOrStoreInfo()`，即对数据的显示或存储操作，以及用于超时警报。
  
* _Bool isKnocking

  由于敲门是由中断函数激活的，因此第一次敲门与第二次敲门之间的时间可能会因程序执行的快慢而不同。该变量可以确保第一次敲门之后，循环中只有记录敲门声的函数会运行，其他的函数会直接跳过，缩短程序从中断函数运行至记录函数的时间，减小误差。



## 图片

#### 连线图

<img src="./FRITZING/Arduino_FINAL_bb.svg">



#### 实物图

<img src="./other/5.png" style="zoom:10%; transform: rotate(90deg); ">

<img src="./other/6.png" style="zoom:10%; transform: rotate(90deg); ">



## 错误与调试记录

#### 敲门的传感器的选用

* 最初想当然的认为敲门的震动应该用震动传感器检测，甚至立项报告中也是这么写的，但直到震动传感器到货了才发现其灵敏度不足以探测到敲门产生的震动(甚至是毫无反应)，因此改用声音传感器
* 声音传感器存在一个问题是扬声器的声音、说话声，甚至伺服电机转动的声音都会激活声音传感器，让Arduino误以为有人敲门。虽然可以通过调节阈值的方式来减小影响，但毕竟还是可能会造成误录。
  * 这种情况下有以下两种方式可以解决误录的问题：
    1. 只记录红外热释传感器探测到有人后的第一次敲门。
    2. 记录下每次的数据，后期在管理EEPROM数据时再删除错误数据。
  * 经过考虑，我采用第一种方式，因为第二次及以后的敲门节奏可能会因为迟迟没开门受到心态影响而改变；此外，由于EEPROM存储空间有限，无法存储很多数据，且事后删除错误数据较为麻烦。
  * 此外，考虑到噪音一般都是短暂出现的，因此噪音的nonVibrationTime[]中全为0，以此可作为噪音的判据，筛除该数据，减小噪音对系统的干扰。
    
      * 代码实现
      
        ```c++
        if (thisInfo.knock[0] == 0) {
          noKnock = 1;
          attachInterrupt(digitalPinToInterrupt(pinSound), startKnock, FALLING);
        }
        ```
      
        


#### 敲门节奏的记录方式

* 开始想用在中断指向的函数中进行敲门节奏的读取，但无法正常实现，查阅资料发现，中断具有以下特点：

  * 不能用``delay()``
  * `millis()的`返回值始终为中断前的值
  * 串口通信会受到干扰

  因此``getVibration()``或``getSound()``的基本功能都无法在中断时实现。

* 解决方法为用一布尔量表示开始敲门，然后在loop()中加入对该布尔量的判断，若为1则进入记录敲门的函数。同时还要跳过其他进程以减少进入记录函数所需时间。

#### 数据库的读取与修改

* ###### 串口通信
  * 开始时串口通信屡屡出错，具体表现在``Serial.available()``的返回值与预期不符。

  经调试发现是由于Serial Monitor中输入模式选择了Newline，改为No line ending后即解决。

  * 此外，串口通信时用``read()``函数读取Serial Monitor的发送数据时接收到的是字符而不是整型变量。

* ###### 异常数据

  * 读取记录的数据时会出现如下异常情况：正常数据结束后会出现异常数据。但记录时存储的数据是正常的。

    读取时的串口监视器：

    <img src="./other/1.png" style="zoom:40%">

    记录时的串口监视器：

    <img src="./other/2.png" style="zoom:40%">

    * 分析发现是数据读取时只判断数据是否为0，而测试数据恰好会导致数组溢出，加入边界判断即可。

#### 红外热释传感器的问题

* 出于未知的原因，我的红外热释传感器的可重复触发模式似乎不可使用，而不可重复触发模式可以使用，这给超时警报功能造成了一定影响。因为有可能在时限到达时红外热释传感器输出的为低电平，这样系统就会误以为来人已经离开而不发出警报。

#### 超声波测距仪的问题(按对测量结果影响从大到小排序)

* ###### 发型干扰

  * 实验发现，若被测男性的发型为短发，则身高会减少近0.5m，而用单手手将头顶覆盖住(确保不是扩散角造成的影响)，则身高测量值又会较为正常；
  * 分析认为，可能是直发男性的头顶相对不平整，容易将超声波反射至别处并造成衰减，使得抵达头皮并反射会测距仪的超声波减少，且头发越长越容易出现这种情况。
  * 因此，有两种建议短发男性采用的方式，一是停留较长时间；二是用手盖住头顶。

* ###### 音速的取值

  * 之前的实验中由于距离较短，音速的不同对结果并不明显。但在此处，由于距离较大，错误的音速取值会带来很大误差；
  * 先前取音速为340m/s时，测得身高比实际值低了约10cm，改取值为348m/s，则身高基本正常；
  * 解释：由于340m/s为15°C下的音速，而笔者完成此项目时正值夏季，室内温度约30°C，不考虑气压影响，音速约为348m/s。

* ###### 测量扩散角问题

  * 超声波测距仪存在15°的扩散角，要求被测平面面积较大。开始时为了美观并且使被测面尽可能大，因此将超声波测距仪布置在屋顶，但此时人头顶面积相对比较小，因此测量身高时会有较大误差(偏大或偏小均有可能，基本在±20cm)；

  <img src="./other/0.jpg" style="zoom:20%">

  * 先后尝试了多种方法，包括在HC-SR04的两个金属管外加装塑料长管试图吸收扩散的超声波来减小扩散角，但并没有效果；
  * 最后只能尽可能降低测距仪安置的高度来减小其影响，但测量较矮的访客的身高时仍会有较大误差；
  * 总体来说，这一现象可能会造成人的实际身高与测量值不匹配，但不同人的测量值之间仍有差异，因此实际上仍可以测量的身高作为身份认证的一个维度。

* ###### 异常数据

  * 出于未知的原因，超声波测距仪的测量数据(距离)有时会为0，因此程序中需加入对此的判断，避免身高测量有误；
  * 有时会出现高度为负的数据，推测是超声波在物体与墙壁之间反射导致其传播时间大于往返于测距仪至地面时间造成的。

#### 数据类型问题

* 代码中多处使用byte型来存储地址，并用-1来表示寻址错误，但byte下-1为255，而后面的判断忽略了这一点，所以出现错误。

#### 伺服电机问题

* 可能由于旋转时不能完全到位，伺服电机在停转后仍会发出响声，并且可能会干扰到声音传感器的识别。
* 解决方法：每次需要用到伺服电机时再attach用完立刻detach。



## 性能分析

* #### 能源

  * ##### 供电

    实验中除舵机以外的其他设备均不需很大的工作电流，而舵机的工作电流也在外接供电线能力之内，因此该系统功能充足。

  * ##### 能耗

    作为门禁系统，大部分时间处于待机状态，只需红外热释传感器在工作即可，而红外热释传感器的静态电流为50μA，因此可近似认为该系统一小时消耗的电量仅为180mAh，一日耗电量为4.32Ah。

* #### 可靠性

  * ##### 硬件

    * ###### 红外热释传感器

      在D3的实验中测试发现灵敏度很高且测量角很大，因此基本不用考虑该传感器犯错。

    * ###### 超声波测高

      超声波测高角度较小，最大不超过15°，因此传感器的位置应在一般人敲门时站的正上方，且若高度越高则有效测量面积就越大。发射面要与水平面垂直。

    * ###### 系统工作时间

      该系统仅有红外热释传感器需长时间工作并传回数据，而红外热释传感器可以长时间工作。

      此外，由于无法断电，声音传感器也会长时间工作。

    * ###### Arduino UNO上EEPROM的可靠性

      经查阅资料，Arduino UNO的EEPROM最多可存储1024Bytes的数据，最多可重复擦写十万次。

  * ##### 软件

    * ###### 节奏感知

      即便是同一个人，不同时间敲门节奏也不可能完全相同，因此需要建模量化处理。初步考虑可以通过对比两次震动之间的时间差、各个时间差占总时间比、震动持续时间(对同一扇门可以体现强度)来进行量化。重复敲门多次可以取第一次的数据，因为第二次敲门可能有心理因素的影响。

    * ###### millis()函数返回值溢出的影响

      计时所需的``millis()``的返回值可能会因时间过久(查阅资料得约为50天)而溢出，但因为millis()仅用于使唤醒一次后系统维持30s的工作状态，因此正好遇到溢出的概率仅有$\frac{30seconds}{50days}$，几乎可以忽略不计；

      同时，若使用unsigned int类型(2 Bytes)来存储，则因为`millis()%65536`的值会有周期性变化，因此相当于经历了一个循环，另加一个判断即可以避免溢出造成影响。

    * ###### 认证

      不能仅通过非密码认证就开门，因为这些都并非无法模仿，因此无法有被信任访客自动解锁功能，除非告知被信任访客开门密码。



## 附件

* #### 外围器件资料

  * [Passive infrared sensor - Wikipedia](https://en.wikipedia.org/wiki/Passive_infrared_sensor)
  * [人体红外传感器(PIR)模块HC-SR501 - 简书](https://www.jianshu.com/p/5a54c38d5f0b)
  * [(SKU:DFR0027)数字震动传感器 - DFRobot Product Wiki](http://wiki.dfrobot.com.cn/index.php/(SKU:DFR0027)数字震动传感器)
  * [(SKU:RB-02S029A)黑色震动传感器 - ALSROBOT WiKi](http://www.alsrobot.cn/wiki/index.php/(SKU:RB-02S029A)黑色震动传感器)
  * [Sound Sensor - Waveshare Wiki](http://www.waveshare.net/wiki/Sound_Sensor)
  * [蓝牙模块HC06资料(密码：9g8a)](https://pan.baidu.com/s/1ivoaYXdEfnNOA4i_yM7U2Q ) 
  * [蓝牙模块HC06购买链接](https://s.taobao.com/search?q=hc05&imgfile=&js=1&stats_click=search_radio_all%3A1&initiative_id=staobaoz_20190717&ie=utf8 )
  * [热释电红外传感器模组的使用与调试](http://www.winsensor.com.cn/hangyexinwen/rsdhwcgqmzdsyyds_519.html)

* #### [bilibili链接](https://www.bilibili.com/video/av65514002)